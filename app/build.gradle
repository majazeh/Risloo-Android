plugins {
    id 'com.android.application' // Android Application
    id 'com.google.gms.google-services' // Google Services
    id 'com.google.firebase.crashlytics' // Firebase Crashlytics
    id 'androidx.navigation.safeargs' // Navigation SafeArgs
}

android {
    compileSdkVersion 32
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.majazeh.risloo"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 9
        versionName "1.7.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

//    signingConfigs {
//        config {
//            storeFile file(SIGNING_STORE_FILE)
//            storePassword SIGNING_STORE_PASSWORD
//            keyAlias SIGNING_KEY_ALIAS
//            keyPassword SIGNING_KEY_PASSWORD
//        }
//    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders.appIcon = "@mipmap/ic_logo_debug"
            manifestPlaceholders.appRoundIcon = "@mipmap/ic_logo_debug_round"
            manifestPlaceholders.appLabel = "ریسلو آزمایشی"
            manifestPlaceholders.appAuthorities = "com.majazeh.risloo.debug.provider"
            manifestPlaceholders.appHost = "dev.risloo.ir"
            manifestPlaceholders.appScheme = "bisloo"
            manifestPlaceholders.appNetworkSecurityConfig = "@xml/network_security_config_debug"
        }

        beta {
            debuggable false
            minifyEnabled true
            shrinkResources true
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
            manifestPlaceholders.appIcon = "@mipmap/ic_logo_beta"
            manifestPlaceholders.appRoundIcon = "@mipmap/ic_logo_beta_round"
            manifestPlaceholders.appLabel = "ریسلو بتا"
            manifestPlaceholders.appAuthorities = "com.majazeh.risloo.beta.provider"
            manifestPlaceholders.appHost = "dev.risloo.ir"
            manifestPlaceholders.appScheme = "bisloo"
            manifestPlaceholders.appNetworkSecurityConfig = "@xml/network_security_config_beta"

//            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile ('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            manifestPlaceholders.appIcon = "@mipmap/ic_logo_release"
            manifestPlaceholders.appRoundIcon = "@mipmap/ic_logo_release_round"
            manifestPlaceholders.appLabel = "ریسلو"
            manifestPlaceholders.appAuthorities = "com.majazeh.risloo.provider"
            manifestPlaceholders.appHost = "risloo.ir"
            manifestPlaceholders.appScheme = "risloo"
            manifestPlaceholders.appNetworkSecurityConfig = "@xml/network_security_config_release"

//            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile ('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ':ligheh')

    /*
    ---------- Androidx Sdk's ----------
    */

    implementation 'androidx.appcompat:appcompat:1.4.1' // Androidx AppCompat
    implementation 'androidx.cardview:cardview:1.0.0' // Androidx CardView
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3' // Androidx ConstraintLayout
    implementation 'androidx.exifinterface:exifinterface:1.3.3' // Androidx ExitInterface
    implementation 'androidx.multidex:multidex:2.0.1' // Androidx MultiDex
    implementation 'androidx.navigation:navigation-fragment:2.4.2' // Androidx Navigation Fragment
    implementation 'androidx.navigation:navigation-ui:2.4.2' // Androidx Navigation Ui
    implementation 'androidx.viewpager2:viewpager2:1.0.0' // Androidx ViewPager2
    implementation 'androidx.recyclerview:recyclerview:1.2.1' // Androidx RecyclerView
    implementation 'androidx.security:security-crypto:1.1.0-alpha03' // Androidx Encrypted SharedPreference
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0' // Androidx SwipeRefreshLayout

    /*
    ---------- Firebase Sdk's ----------
    */

    implementation platform('com.google.firebase:firebase-bom:29.0.1') // Firebase Bom

    implementation 'com.google.firebase:firebase-crashlytics' // Firebase Crashlytics
    implementation 'com.google.firebase:firebase-analytics' // Firebase Analytics

    /*
    ---------- SMS Verification Sdk's ----------
    */

    implementation 'com.google.android.gms:play-services-auth:20.2.0' // GMS Auth
    implementation 'com.google.android.gms:play-services-auth-api-phone:18.0.1' // GMS Api Phone

    /*
    ---------- Google Sdk's ----------
    */

    implementation 'com.google.android.material:material:1.5.0' // Google Materials
    implementation 'com.google.code.gson:gson:2.9.0' // Google GSON

    /*
     ---------- Intuit Sdk's ----------
    */

    implementation 'com.intuit.sdp:sdp-android:1.0.6' // Intuit Android sdp
    implementation 'com.intuit.ssp:ssp-android:1.0.6' // Intuit Android ssp

    /*
    ---------- Github Sdk's ----------
    */

    implementation 'com.github.chrisbanes:PhotoView:2.3.0' // Github PhotoView
    implementation 'com.github.samanzamani:PersianDate:1.1' // Github PersianDate
    implementation 'com.github.yalantis:ucrop:2.2.7' // Github UCropView

    /*
    ---------- SquareUp Sdk's ----------
    */

    implementation 'com.squareup.picasso:picasso:2.71828' // SquareUp Picasso

    /*
    ---------- Facebook Sdk's ----------
    */

    implementation 'com.facebook.shimmer:shimmer:0.5.0' // Facebook Shimmer

    /*
    ---------- Others Sdk's ----------
    */

    implementation 'de.hdodenhof:circleimageview:3.1.0' // Hdodenhof CircleImageView
    implementation 'io.noties.markwon:core:4.6.2' // Noties MarkDownView

    implementation files('../libs/bouncy-release.aar') // Recyclerview Bouncy

    /*
    ---------- Tests Sdk's ----------
    */

    testImplementation 'junit:junit:4.13.2' // Unit Test
    testImplementation 'com.google.truth:truth:1.1.3' // Local Assertion

    androidTestImplementation 'androidx.test.ext:junit:1.1.3' // Instrumentation Test
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0' // Ui Test
    androidTestImplementation 'com.google.truth:truth:1.1.3' // Android Assertion

}