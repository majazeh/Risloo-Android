plugins {
    id 'com.android.application' // Android Build Application
    id 'androidx.navigation.safeargs' // Navigation SafeArgs
}

def keystorePropertiesFile = rootProject.file("keystore.properties") // Initialize RootProject File
def keystoreProperties = new Properties() // Initialize New Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile)) // Set KeystoreProperties Object.

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.majazeh.risloo"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug{
            debuggable true
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders.appIcon = "@mipmap/ic_logo_debug"
            manifestPlaceholders.appRoundIcon = "@mipmap/ic_logo_debug_round"
            manifestPlaceholders.appLabel = "ریسلو آزمایشی"
            manifestPlaceholders.appAuthorities = "com.majazeh.risloo.debug.fileprovider"
        }

        beta {
            debuggable false
            minifyEnabled true
            shrinkResources true
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
            manifestPlaceholders.appIcon = "@mipmap/ic_logo_beta"
            manifestPlaceholders.appRoundIcon = "@mipmap/ic_logo_beta_round"
            manifestPlaceholders.appLabel = "ریسلو بتا"
            manifestPlaceholders.appAuthorities = "com.majazeh.risloo.beta.fileprovider"
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile ('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            manifestPlaceholders.appIcon = "@mipmap/ic_logo_release"
            manifestPlaceholders.appRoundIcon = "@mipmap/ic_logo_release_round"
            manifestPlaceholders.appLabel = "ریسلو"
            manifestPlaceholders.appAuthorities = "com.majazeh.risloo.fileprovider"
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile ('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ':ligheh')

    implementation files('../libs/bouncy-release.aar')

    implementation 'androidx.appcompat:appcompat:1.3.1' // AppCompat
    implementation 'androidx.cardview:cardview:1.0.0' // CardView
    implementation 'androidx.multidex:multidex:2.0.1' // MultiDex
    implementation 'androidx.viewpager2:viewpager2:1.0.0' // ViewPager2
    implementation 'androidx.recyclerview:recyclerview:1.2.1' // RecyclerView
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0' // ConstraintLayout
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0' // SwipeRefreshLayout
    implementation 'androidx.navigation:navigation-fragment:2.3.5' // Navigation Fragment
    implementation 'androidx.navigation:navigation-ui:2.3.5' // Navigation Ui

    implementation 'com.google.android.material:material:1.4.0' // Google Materials
    implementation 'com.google.code.gson:gson:2.8.7' // Google GSON

    implementation 'com.github.chrisbanes:PhotoView:2.3.0' // ZoomImageView
    implementation 'com.github.samanzamani:PersianDate:1.1' // Persian Date

    implementation 'com.facebook.shimmer:shimmer:0.5.0' // Facebook Shimmer

    implementation 'com.squareup.picasso:picasso:2.71828' // SquareUp Picasso

    implementation 'com.intuit.sdp:sdp-android:1.0.6' // Responsive dp
    implementation 'com.intuit.ssp:ssp-android:1.0.6' // Responsive sp

    implementation 'de.hdodenhof:circleimageview:3.1.0' // CircleImageView
    implementation 'io.noties.markwon:core:4.6.2' // MarkDownView

    testImplementation 'junit:junit:4.13.2' // Unit Framework

    androidTestImplementation 'androidx.test.ext:junit:1.1.3' // Unit Test
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0' // Espresso Core
}