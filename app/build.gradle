plugins {
    id 'com.android.application' // Android Application
    id 'com.google.gms.google-services' // Google Services
    id 'com.google.firebase.crashlytics' // Firebase Crashlytics
    id 'androidx.navigation.safeargs' // Navigation SafeArgs
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.majazeh.risloo"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 9
        versionName "1.7.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            storeFile file(SIGNING_STORE_FILE)
            storePassword SIGNING_STORE_PASSWORD
            keyAlias SIGNING_KEY_ALIAS
            keyPassword SIGNING_KEY_PASSWORD
        }
    }

    buildTypes {
        debug{
            debuggable true
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders.appIcon = "@mipmap/ic_logo_debug"
            manifestPlaceholders.appRoundIcon = "@mipmap/ic_logo_debug_round"
            manifestPlaceholders.appLabel = "ریسلو آزمایشی"
            manifestPlaceholders.appAuthorities = "com.majazeh.risloo.debug.provider"
            manifestPlaceholders.appHost = "dev.risloo.ir"
            manifestPlaceholders.appScheme = "bisloo"
            manifestPlaceholders.appNetworkSecurityConfig = "@xml/network_security_config_debug"
        }

        beta {
            debuggable false
            minifyEnabled true
            shrinkResources true
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
            manifestPlaceholders.appIcon = "@mipmap/ic_logo_beta"
            manifestPlaceholders.appRoundIcon = "@mipmap/ic_logo_beta_round"
            manifestPlaceholders.appLabel = "ریسلو بتا"
            manifestPlaceholders.appAuthorities = "com.majazeh.risloo.beta.provider"
            manifestPlaceholders.appHost = "dev.risloo.ir"
            manifestPlaceholders.appScheme = "bisloo"
            manifestPlaceholders.appNetworkSecurityConfig = "@xml/network_security_config_beta"

            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile ('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            manifestPlaceholders.appIcon = "@mipmap/ic_logo_release"
            manifestPlaceholders.appRoundIcon = "@mipmap/ic_logo_release_round"
            manifestPlaceholders.appLabel = "ریسلو"
            manifestPlaceholders.appAuthorities = "com.majazeh.risloo.provider"
            manifestPlaceholders.appHost = "risloo.ir"
            manifestPlaceholders.appScheme = "risloo"
            manifestPlaceholders.appNetworkSecurityConfig = "@xml/network_security_config_release"

            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile ('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ':ligheh')

    /*
     ---------- Firebase Api's ----------
    */

    implementation platform('com.google.firebase:firebase-bom:29.0.1') // Firebase Bom

    implementation 'com.google.firebase:firebase-crashlytics' // Crashlytics
    implementation 'com.google.firebase:firebase-analytics' // Analytics

    /*
     ---------- SMS Verification Api's ----------
    */

    implementation 'com.google.android.gms:play-services-auth:19.2.0' // Auth
    implementation 'com.google.android.gms:play-services-auth-api-phone:17.5.1' // Auth Phone

    /*
     ---------- Androidx Api's ----------
    */

    implementation 'androidx.appcompat:appcompat:1.4.0' // AppCompat
    implementation 'androidx.cardview:cardview:1.0.0' // CardView
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2' // ConstraintLayout
    implementation 'androidx.exifinterface:exifinterface:1.3.3' // ExitInterface
    implementation 'androidx.multidex:multidex:2.0.1' // MultiDex
    implementation 'androidx.navigation:navigation-fragment:2.3.5' // Navigation Fragment
    implementation 'androidx.navigation:navigation-ui:2.3.5' // Navigation Ui
    implementation 'androidx.viewpager2:viewpager2:1.0.0' // ViewPager2
    implementation 'androidx.recyclerview:recyclerview:1.2.1' // RecyclerView
    implementation 'androidx.security:security-crypto:1.1.0-alpha03' // Encrypted SharedPreference
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0' // SwipeRefreshLayout

    /*
     ---------- Responsive Api's ----------
    */

    implementation 'com.intuit.sdp:sdp-android:1.0.6' // Responsive dp
    implementation 'com.intuit.ssp:ssp-android:1.0.6' // Responsive sp

    /*
    ---------- Others Api's ----------
   */

    implementation 'com.google.android.material:material:1.4.0' // Google Materials
    implementation 'com.google.code.gson:gson:2.8.8' // Google GSON

    implementation 'com.github.chrisbanes:PhotoView:2.3.0' // ZoomImageView
    implementation 'com.github.samanzamani:PersianDate:1.1' // Persian Date
    implementation 'com.github.yalantis:ucrop:2.2.7' // Urop Image

    implementation 'com.facebook.shimmer:shimmer:0.5.0' // Facebook Shimmer

    implementation 'com.squareup.picasso:picasso:2.71828' // SquareUp Picasso

    implementation 'de.hdodenhof:circleimageview:3.1.0' // CircleImageView
    implementation 'io.noties.markwon:core:4.6.2' // MarkDownView

    implementation files('../libs/bouncy-release.aar') // Recyclerview Bouncy

    /*
    ---------- Test Api's ----------
   */

    testImplementation 'junit:junit:4.13.2' // Unit Framework

    androidTestImplementation 'androidx.test.ext:junit:1.1.3' // Unit Test
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0' // Espresso Core
}